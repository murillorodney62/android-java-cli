plugins {
	id 'java'
}

defaultTasks 'clean', 'build', 'getDeps', 'buildZip'

description = 'Structurizr CLI'
group = 'com.structurizr'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {

	if (project.hasProperty("local")) {
		implementation fileTree(dir: 'lib', include: ['*.jar'])
		implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'
		implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
		implementation 'javax.xml.bind:jaxb-api:2.3.1'
	} else {
		implementation 'com.structurizr:structurizr-dsl:1.20.0'
		implementation 'com.structurizr:structurizr-export:1.6.1'
		implementation 'com.structurizr:structurizr-graphviz:1.6.1'
	}

	implementation 'commons-cli:commons-cli:1.5.0'
	implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'
	implementation 'org.codehaus.groovy:groovy-jsr223:3.0.12'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.7.10'
    implementation 'org.jruby:jruby-core:9.3.6.0'

	implementation 'commons-logging:commons-logging:1.2'
	implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
	implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
	implementation 'org.apache.logging.log4j:log4j-jcl:2.19.0'
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'

}

test {
	useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': description,
                'Implementation-Version': version
        )
    }
}

task getDeps(type: Copy) {
	from configurations.default
	into 'build/dependencies'
}

task buildZip(type: Zip) {
	from ('build/libs/') {
		include 'structurizr-cli*.jar'
		into 'lib'
	}
	from ('build/dependencies') {
		include '*.jar'
		into 'lib'
	}
	from ('.') {
		include 'README.md'
		include 'docs/*'
		include 'docs/images/*'
	}
	from ('etc') {
		include 'structurizr.sh'
		include 'structurizr.bat'
	}
}